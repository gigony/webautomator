
//buildscript {
//
//  repositories {
//    mavenCentral()
//  }
//
//  dependencies {
////    classpath group: "net.carlosgsouza", name: "gradle-console", version: "1.+"
////      classpath 'de.undercouch:gradle-download-task:1.2'
//  }
//
//}

plugins {
    id "de.undercouch.download" version "1.2"
}

apply plugin: "idea"

allprojects {
  apply plugin: "java"
  apply plugin: 'maven'
  group = 'com.gigony.qte'
  version = "0.1-SNAPSHOT"

  repositories {
    mavenCentral()
  }


  def isMavenDeployable = project.hasProperty('signing.keyId') &&
                          project.hasProperty('signing.password') &&
                          project.hasProperty('signing.secretKeyRingFile') &&
                          project.hasProperty('ossrhUsername') &&
                          project.hasProperty('ossrhPassword')
  if (isMavenDeployable) {
    if (!version.endsWith('-SNAPSHOT')) {
      apply plugin: 'signing'
      signing {
        sign configurations.archives
      }
    }

    uploadArchives {
      repositories {
        mavenDeployer {
          beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

          repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
          }

          snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
          }

          pom.project {
            name 'Quick Test Enabler'
            packaging 'jar'
            // optionally artifactId can be defined here
            description 'A Framework for Web automation and testing'
            url 'https://github.com/gigony/webautomator'

            scm {
              connection 'scm:git:git@github.com:gigony/webautomator.git'
              developerConnection 'scm:git:git@github.com:gigony/webautomator.git'
              url 'https://github.com/gigony/webautomator'
            }

            licenses {
              license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              }
            }

            developers {
              developer {
                id 'gigony'
                name 'Gigon Bae'
                email 'gigony@gmail.com'
              }
            }
          }
        }
      }
    }
  }
}

subprojects {

  configurations {
    markdownDoclet
  }
  dependencies {
    testCompile group: "junit", name: "junit", version: "[4.0,)"
    markdownDoclet 'ch.raffael.pegdown-doclet:pegdown-doclet:1.1.1'
  }

  [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

  // force tests
  // test.outputs.upToDateWhen { false }

  javadoc {
    source = sourceSets.main.allJava
    options {
      docletpath = configurations.markdownDoclet.files.asType(List) // gradle should relly make this simpler
      doclet = "ch.raffael.doclets.pegdown.PegdownDoclet"
      addStringOption("parse-timeout", "10")
    }
  }

  task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
  }

  task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives javadocJar, sourcesJar
  }
}


archivesBaseName = "webautomator-all"

//subprojects.each { subproject -> evaluationDependsOn(subproject.path)}



task javadocJar(type: Jar, dependsOn: subprojects.assemble) {
  classifier = 'javadoc'
  subprojects.each { subproject ->
      from subproject.javadoc
  }
}

task sourcesJar(type: Jar, dependsOn: subprojects.assemble) {
  classifier = 'sources'
  subprojects.each { subproject ->
    from subproject.sourceSets.main.allSource
  }
}


artifacts {
  archives javadocJar, sourcesJar
}


dependencies {
  compile project(':webautomator-core')
}

task fatJar(type: Jar, dependsOn: subprojects.assemble) {
//  manifest {
//    attributes 'Main-Class': ''
//  }
  baseName = project.name + '-fat'
  subprojects.each { subproject ->
    from {subproject.sourceSets.main.output}
    from {subproject.sourceSets.main.allSource}
    from {subproject.configurations.compile.collect { it.isDirectory() ? it: zipTree(it)} }
  }
  // remove all signature files
  exclude "META-INF/*.SF"
  exclude "META-INF/*.DSA"
  exclude "META-INF/*.RSA"
  with jar
}


//mainClassName = "edu.unl.qte.webautomator.Main"

task getHomeDir << {
    println gradle.gradleHomeDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
